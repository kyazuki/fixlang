module AsyncTask;
// This module provides a way to run a task asynchronously.
// 
// Importing this module automatically enables `--threaded` flag of the compiler. 
// Note that this causes some overhead even for a single-threaded program.

// A type for a computation task that can be run asynchronously.
type Task a = unbox struct { dtor : Destructor (Ptr, TaskData a) };

// A type to store a task and its result. 
// This type should be used only by implementation of this library.
// TODO: We can change this to enum to release task function earlier.
type TaskData a = box struct {
    _func : () -> Array a, // We need a type of boxed `Option`, and actually we use `Array` of length at most one instead.
    _result : Array a
};

// Make a task which performs a computation asynchronously.
// The first boolean argument specifies whether the task should be executed in a dedicated thread or in the thread pool.
_make_inner : Bool -> (() -> a) -> Task a;
_make_inner = |on_dedicated_thread, func| (
    let func = mark_threaded(func);
    let task_data = TaskData { _func : |_| [func()], _result : [] };
    let on_dedicated_thread = if on_dedicated_thread { 1_U8 } else { 0_U8 };
    let task_handle = CALL_C[Ptr fixruntime_threadpool_create_task(Ptr, U8), task_data._unsafe_get_ptr_of_boxed_value, on_dedicated_thread];
    let dtor = Destructor::make((task_handle, task_data), |(task_handle, task_data)| (
        eval CALL_C[() fixruntime_threadpool_delete_task(Ptr), task_handle];
        eval task_data; // Do not deallocate `task_data` before calling `fixruntime_threadpool_delete_task`.
        ()
    ));
    Task { dtor : dtor }
);

// Get the result of a computation task.
// This function blocks the current thread until the task is finished.
get : Task a -> a;
get = |task| (
    let (task_handle, task_data) = task.@dtor.@value;
    eval CALL_C[() fixruntime_threadpool_wait_task(Ptr), task_handle];
    eval task; // Do not call destructor of `task` before calling `fixruntime_threadpool_wait_task`.
    task_data.@_result.@(0) // TODO: Change this to _unsafe_get_unchecked.
);

// Make a task which performs a computation asynchronously.
// The task is queued and will be executed in a thread pool later.
// NOTE: 
// Parallel nor preemptive multitasking is not guaranteed.
// In particular, when a thread starts to wait the result of a task which is still not running, then it will be executed in the waiting thread itself.

// Example:
// ```
// module Main;
// import AsyncTask;
// import Debug;

// main : IO ();
// main = (
//     let sum_range = |from, to| (
//         loop((0, from), |(sum, i)| (
//             if i == to { break $ sum };
//             continue $ (sum + i, i + 1)
//         ))
//     );
//     let n = 1000000000;
//     // Compute the sum of numbers from 0 to n/2 - 1.
//     // This task will be executed asynchronously (if you are using multi-core CPU).
//     let sum_former = AsyncTask::make(|_| sum_range(0, n/2));
//     // Compute the sum of numbers from n/2 to n.
//     // We perfom this in the current thread while waiting for the result of the former task.
//     let sum_latter = sum_range(n/2, n);
//     // Sum up the results of the two computations.
//     let sum = sum_former.get + sum_latter;
//     // Then the sum should be n * (n - 1) / 2.
//     eval assert_eq(|_|"", sum, n * (n - 1) / 2);
//     println $ 
//         "Sum of numbers from 0 to " + (n - 1).to_string + 
//         " is " + sum_former.get.to_string + " + " + sum_latter.to_string + 
//         " = " + sum.to_string + "."
// );
// ```
make : (() -> a) -> Task a;
make = |func| _make_inner(false, func);

// Make a task which performs a computation asynchronously.
// In contrast to `make`, the task created by this function is executed in a dedicated thread.
make_on_dedicated_thread : (() -> a) -> Task a;
make_on_dedicated_thread = |func| _make_inner(true, func);

// Get the number of processors (CPU cores) currently available.
// This is implemented by calling `sysconf(_SC_NPROCESSORS_ONLN)`.
// The runtime pools as many threads as this number to execute asynchronous tasks.
number_of_processors : I64;
number_of_processors = CALL_C[I64 fixruntime_get_number_of_processors()];

namespace AsyncIOTask {

    // A type for an I/O action that can be run asynchronously.
    type IOTask a = unbox struct { _task : Task a };

    // Get the result of an asynchronous I/O action.
    get : IOTask a -> IO a;
    get = |IOTask { _task : task }| IO { _data : |_| task.get };

    // An `IO` version of `AsyncTask::make`.
    // 
    // Example:
    // ```
    // module Main;
    // import AsyncTask;
    // 
    // main : IO ();
    // main = (
    //     let print_ten : I64 -> IO () = |task_num| (
    //         loop_m(0, |i| (
    //             if i == 10 {
    //                 break_m $ ()
    //             } else {
    //                 let msg = "task number: " + task_num.to_string + ", i: " + i.to_string;
    //                 eval *msg.println;
    //                 continue_m $ i + 1
    //             }
    //         ))
    //     );
    //     let task_0 = AsyncIOTask::make(print_ten(0));
    //     let task_1 = AsyncIOTask::make(print_ten(1));
    //     eval *task_0.get;
    //     eval *task_1.get;
    //     pure()
    // );
    // ```
    make : IO a -> IOTask a;
    make = |io| IOTask { _task : make(|_| io._unsafe_perform) };

    // An `IO` version of `AsyncTask::make_on_dedicated_thread`.
    make_on_dedicated_thread : IO a -> IOTask a;
    make_on_dedicated_thread = |io| IOTask { _task : make_on_dedicated_thread(|_| io._unsafe_perform) };
}
