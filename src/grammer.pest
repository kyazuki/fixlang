sep = _{ " " | "\t" | NEWLINE }

int_expr = { "-"? ~ ASCII_DIGIT ~ ASCII_DIGIT* }

lit_expr = { int_expr }

keywords = _{ "let" | "in" | "if" | "then" | "else" }

var_char = _{ ASCII_ALPHA | ASCII_DIGIT }

var_expr = { !keywords ~ (ASCII_ALPHA_LOWER ~ var_char*) }

let_expr = { "let" ~ sep+ ~ var_expr ~ sep* ~ "=" ~ sep* ~ expr ~ sep* ~ "in" ~ sep* ~ expr }

if_expr = { "if" ~ sep+ ~ expr ~ sep+ ~ "then" ~ sep+ ~ expr ~ sep+ ~ "else" ~ sep+ ~ expr }

lam_expr = { "\\" ~ sep* ~ var_expr ~ sep* ~ "->" ~ sep* ~ expr }

app_expr = { not_app_expr ~ (sep+ ~ not_app_expr)+ }

not_app_expr = { lit_expr | var_expr | let_expr | if_expr | lam_expr | bracket_expr }

bracket_expr = { "(" ~ sep* ~ expr ~ sep* ~ ")" }

expr = { app_expr | not_app_expr }

file = _{ SOI ~ sep* ~ expr ~ sep* ~ EOI }