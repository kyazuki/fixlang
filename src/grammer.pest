// === Basic rules ===
sep = _{ " " | "\t" | NEWLINE | block_comment | line_comment }

int_lit_expr = { "-"? ~ ASCII_DIGIT ~ ASCII_DIGIT* }

bool_lit_expr = { "true" | "false" }

lit_expr = { int_lit_expr | bool_lit_expr }

keywords = _{ "let" | "in" | "if" | "then" | "else" | bool_lit_expr }

// === Expressions ===
let_in = _{ "in" | ";" }

var_char = _{ ASCII_ALPHA | ASCII_DIGIT }

var_expr = { !keywords ~ (ASCII_ALPHA_LOWER ~ var_char* ~ "!"? ) }

var_with_type = { var_expr ~ sep* ~ ":" ~ sep* ~ type_expr }

let_expr = { "let" ~ sep+ ~ var_expr ~ sep* ~ "=" ~ sep* ~ expr ~ sep* ~ let_in ~ sep* ~ expr }

if_expr = { "if" ~ sep+ ~ expr ~ sep+ ~ "then" ~ sep+ ~ expr ~ sep+ ~ "else" ~ sep+ ~ expr }

lam_expr = { "\\" ~ sep* ~ var_with_type ~ sep*  ~ "->" ~ sep* ~ expr }

forall_expr = { "for" ~ sep* ~ "<" ~ sep* ~ type_var ~ ("," ~ sep* ~ type_var)* ~ sep* ~ ">" ~ sep* ~ expr }

bracket_expr = { "(" ~ sep* ~ expr ~ sep* ~ ")" }

// nlc = non left recursive
expr_nlc = { lit_expr | var_expr | let_expr | if_expr | lam_expr | forall_expr | bracket_expr }

tyapp_bracket = { "<" ~ sep* ~ type_expr ~ ("," ~ sep* ~ type_expr)* ~ sep* ~ ">" }

expr_nlc_tyapp = { expr_nlc ~ (sep* ~ tyapp_bracket)? }

expr_app_seq = { expr_nlc_tyapp ~ (sep+ ~ expr_nlc_tyapp)* }

expr = { expr_app_seq }

// === Types ===
type_expr = { type_app | type_fun | type_except_app_fun }

type_bracket = { "(" ~ sep* ~ type_expr ~ sep* ~ ")" }

type_except_app_fun = { type_var | type_tycon_app | type_lit | type_forall | type_bracket }
// type_tycon_app has to precede to type_lit

type_except_fun = { type_app | type_except_app_fun }

type_var = { ASCII_ALPHA_LOWER ~ var_char* }

type_lit = { ASCII_ALPHA_UPPER ~ var_char* }

type_app = { type_except_app_fun ~ sep* ~ "<" ~ sep* ~ type_expr ~ ("," ~ sep* ~ type_expr)* ~ sep* ~ ">" }

type_tycon_app = { tycon ~ sep* ~ "<" ~ sep* ~ type_expr ~ ("," ~ sep* ~ type_expr)* ~ sep* ~ ">" }

type_fun = { type_except_fun ~ sep* ~ "=>" ~ sep* ~ type_expr }

type_forall = { "for" ~ sep* ~ "<" ~ sep* ~ type_var ~ ("," ~ sep* ~ type_var)* ~ sep* ~ ">" ~ sep* ~ type_expr }

tycon = { ASCII_ALPHA_UPPER ~ var_char* }

// === Comments ===
block_comment = _{ "{-"  ~ block_commented_character*  ~ "-}" }

block_commented_character = _{ !"-}" ~ ANY }

line_comment = _{"//" ~ line_commented_character* ~ ( NEWLINE | EOI )}

line_commented_character = _{ !(NEWLINE | EOI) ~ ANY }

// === File ===
file = _{ SOI ~ sep* ~ expr ~ sep* ~ EOI }